plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}


group = 'net.zithium'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    // External APIs
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.3"

    // Internal APIs
    implementation "me.mattstudios.utils:matt-framework:1.4.6"
    implementation 'dev.triumphteam:triumph-gui:3.1.5'

}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

build {
    dependsOn(shadowJar)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    minimize()
    archiveFileName = "ZithiumChatColor-${version}.jar"
    relocate 'me.mattstudios.mf', 'net.zithium.chatcolor.lib.command'
    relocate 'dev.triumphteam.gui', 'net.zithium.chatcolor.libs.gui'
}